;; arith ;;

(rewrite ; constant fold for arith.addf
    (arith_addf
        (arith_constant (NamedAttr "value" (FloatAttr ?x ?t)) ?t)
        (arith_constant (NamedAttr "value" (FloatAttr ?y ?t)) ?t)
        ?a ?t)
    (arith_constant (NamedAttr "value" (FloatAttr (+ ?x ?y) ?t)) ?t)
)

(rewrite ; commute for arith.addf
    (arith_addf ?x ?y ?a ?t)
    (arith_addf ?y ?x ?a ?t)
)

(rule ; division by a power of 2 is equivalent to right shift
    ((= ?lhs (arith_divsi ?x (arith_constant (NamedAttr "value" (IntegerAttr ?n ?t)) ?t) ?t)) ; x / n
     (= ?lgn (log2 ?n)) ; if n = 2^k, then k = log2(n)
     (= ?n (<< 1 ?lgn))) ; check if n is a power of 2
    ((union ?lhs (arith_shrsi ?x (arith_constant (NamedAttr "value" (IntegerAttr ?lgn ?t)) ?t) ?t))) ; x / n = x >> lgn
)

(rule ; multiplication by a power of 2 is equivalent to left shift
    ((= ?lhs (arith_muli ?x (arith_constant (NamedAttr "value" (IntegerAttr ?n ?t)) ?t) ?a ?t)) ; x * n
     (= ?lgn (log2 ?n)) ; if n = 2^k, then k = log2(n)
     (= ?n (<< 1 ?lgn))) ; check if n is a power of 2
    ((union ?lhs (arith_shli ?x (arith_constant (NamedAttr "value" (IntegerAttr ?lgn ?t)) ?t) ?a ?t))) ; x * n = x << lgn
)

;; linalg ;;

(rewrite ; double transpose
    (linalg_transpose (linalg_transpose ?x ?y ?perm ?t1) ?z ?perm ?t2)
    ?x
)

; Analysis for cost of linalg_matmul
(function nrows (Op) i64)
(function ncols (Op) i64)

(rule ((= ?m (Value ?id (RankedTensor ?dims ?t))) (> (vec-length ?dims) 1))
    ((set (nrows ?m) (vec-get ?dims 0))
        (set (ncols ?m) (vec-get ?dims 1))))

(rule ((= ?m (tensor_empty (RankedTensor ?dims ?t))) (> (vec-length ?dims) 1))
    ((set (nrows ?m) (vec-get ?dims 0))
        (set (ncols ?m) (vec-get ?dims 1))))

(rule ((= ?m (linalg_matmul ?x ?y ?o (RankedTensor ?d ?t)))
        (= ?a (nrows ?x))
        (= ?b (ncols ?x))
        (= ?c (ncols ?y)))
    ((set (nrows ?m) ?a)
        (set (ncols ?m) ?c)
        (unstable-cost (linalg_matmul ?x ?y ?o (RankedTensor ?d ?t)) (* (* ?a ?b) ?c))))

(rewrite
    (linalg_matmul (linalg_matmul ?x ?y ?it1 (RankedTensor ?ds1 ?t)) ?z ?it2 (RankedTensor ?ds2 ?t))
    (linalg_matmul ?x (linalg_matmul ?y ?z (tensor_empty (RankedTensor (vec-of (nrows ?y) (ncols ?z)) ?t)) (RankedTensor (vec-of (nrows ?y) (ncols ?z)) ?t)) ?it2 (RankedTensor ?ds2 ?t))
)

(rule
    ((= ?lhs (linalg_matmul (linalg_matmul ?x ?y ?o0 (RankedTensor ?d0 ?t)) ?z ?o1 (RankedTensor ?d1 ?t))))
    ((let out_t (RankedTensor (vec-of (nrows ?y) (ncols ?z)) ?t))
     (union ?lhs (linalg_matmul ?x (linalg_matmul ?y ?z (tensor_empty out_t) out_t) ?o1 (RankedTensor ?d1 ?t))))
)
